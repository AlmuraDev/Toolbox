plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.minecrell.licenser' version '0.3'
}

group 'com.almuradev'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-parameters']
    options.deprecation = true
    options.encoding = 'UTF-8'
}

license {
    header project.file('header.txt')
    include '**/*.java'
    newLine false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'spongepowered'
        url 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    compile 'net.kyori:lunar:1.0.6'
    compile 'net.kyori:membrane:1.0.6'
    compile 'net.kyori:violet:1.1.0'
    compile group: 'org.spongepowered', name: 'spongeapi', version: sponge
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') }
    sign configurations.archives
}

jar {
    manifest.attributes(
        'Automatic-Module-Name': 'com.almuradev.toolbox',
        'Built-By': System.properties['user.name']
    )
}

if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    description 'Shared code useful for anyone working with the Sponge ecosystem'
                    name project.name
                    url 'https://github.com/AlmuraDev/Toolbox/'

                    developers {
                        developer {
                            name 'kashike'
                        }
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/AlmuraDev/Toolbox/issues'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    scm {
                        connection 'scm:git@github.com:AlmuraDev/Toolbox.git'
                        developerConnection 'scm:git@github.com:AlmuraDev/Toolbox.git'
                        url 'https://github.com/AlmuraDev/Toolbox/'
                    }
                }
            }
        }
    }
}
